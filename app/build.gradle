plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
}

apply from: 'shaderConsts.gradle'

android {
    namespace 'net.zatrit.skinbread'
    compileSdk targetSdk

    defaultConfig {
        applicationId 'net.zatrit.skinbread'
        testApplicationId "${applicationId}.test"

        minSdk 30
        targetSdk targetSdk
        versionCode 1
        versionName '1.0'

        resConfigs 'en', 'ru'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles 'proguard-rules.pro', 'modified-android-optimize.pro'
        }

        staging {
            initWith release
            signingConfig signingConfigs.debug
        }
    }

    packagingOptions {
        exclude '**/kotlin-tooling-metadata.json'
        exclude '**/kotlin/**'

        dex {
            useLegacyPackaging true
        }
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    kotlinOptions {
        jvmTarget = project.java_version
        // https://www.guardsquare.com/blog/eliminating-data-leaks-caused-by-kotlin-assertions
        freeCompilerArgs += [
                '-Xno-call-assertions',
                '-Xno-receiver-assertions',
                '-Xno-param-assertions'
        ]
    }

    testOptions {
        reportDir "$rootDir/test-reports"
        resultsDir "$rootDir/test-results"
    }
}

dependencies {
    implementation project(':skinlib')

    testImplementation 'org.jetbrains.kotlin:kotlin-test'

    androidTestImplementation "androidx.test:core:$test_core_version"
    androidTestImplementation "androidx.test:runner:$test_runner_version"
    androidTestImplementation "androidx.test:rules:$test_rules_version"
}
